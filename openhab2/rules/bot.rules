import java.time.Instant
import java.time.Duration

val BOILER_DET_POWER = 1000
var boolean silence_boiler_heated_flag = true
var boolean room_heated60_flag = false
var boolean room_heated75_flag = false
var Instant very_high_power_timestamp = Instant.MIN
var Instant silence_high_power_timestamp = Instant.MIN
var Instant silence_reboot_timestamp = Instant.now()

rule "System rebooted telegram"
when
  System started
then
  silence_boiler_heated_flag = true
  room_heated60_flag = false
  room_heated75_flag = false
  very_high_power_timestamp = Instant.MIN
  silence_high_power_timestamp = Instant.MIN
  sendTelegram("bot1", "Перезагрузка системы")
end

rule "Box treg telegram"
when
  Item c2_treg_on_relay changed
then
  if (Duration.between(silence_reboot_timestamp, Instant.now()).getSeconds() < 8) { return; }
  sendTelegram("bot1", "Терморегулятор подогрева шкафа " + if (c2_treg_on_relay.state == ON) "активирован" else "отключен")
end

rule "Convector telegram"
when
  Item c3_relay1 changed
then
  if (Duration.between(silence_reboot_timestamp, Instant.now()).getSeconds() < 8) { return; }
  if (c3_treg_on_relay1.state == ON) { return; }
  var String s
  if (c3_relay1.state == ON) {
    s = "включена"
  } else {
    s = "выключена"
    silence_high_power_timestamp = Instant.now()
  }
  sendTelegram("bot1", "Батарея " + s)
end

rule "Convector treg telegram"
when
  Item c3_treg_on_relay1 changed
then
  if (Duration.between(silence_reboot_timestamp, Instant.now()).getSeconds() < 8) { return; }
  sendTelegram("bot1", "Терморегулятор батареи " + if (c3_treg_on_relay1.state == ON) "активирован" else "отключен")
end

rule "Boiler telegram"
when
  Item c4_relay1 changed
then
  if (Duration.between(silence_reboot_timestamp, Instant.now()).getSeconds() < 8) { return; }
  var String s
  if (c4_relay1.state == ON) {
    s = "включен"
  } else {
    s = "выключен"
    silence_boiler_heated_flag = true
    silence_high_power_timestamp = Instant.now()
  }
  sendTelegram("bot1", "Бойлер " + s)
end

rule "Tepl.pol telegram"
when
  Item c4_relay2 changed
then
  if (Duration.between(silence_reboot_timestamp, Instant.now()).getSeconds() < 8) { return; }
  var String s
  if (c4_relay2.state == ON) {
    s = "включен"
  } else {
    s = "выключен"
    silence_high_power_timestamp = Instant.now()
  }
  sendTelegram("bot1", "Теплый пол " + s)
end

rule "Boiler heated and high powered telegram"
when
  Item p1_p changed
then
  val Instant cur_t = Instant.now()
  //if (Duration.between(silence_reboot_timestamp, cur_t).getSeconds() < 8) { return; }
  val p = p1_p.state
  if (c4_relay1.state == ON) {
    if (p >= BOILER_DET_POWER) {
      silence_boiler_heated_flag = false
    } else {
      if (!silence_boiler_heated_flag) {
        sendTelegram("bot1", "Бойлер нагрелся!")
        silence_boiler_heated_flag = true
      }
    }
  }
  if (p >= 450 && c3_relay1.state != ON && c4_relay1.state != ON && c4_relay2.state != ON
      && Duration.between(very_high_power_timestamp, cur_t).toMinutes() >= 60
      && Duration.between(silence_high_power_timestamp, cur_t).getSeconds() >= 5) {
    sendTelegram("bot1", "Что-то включено (" + p + "Вт)")
    very_high_power_timestamp = cur_t
  }
end

rule "Room heated telegram"
when
  Item c3_temp_out changed
then
  if (Duration.between(silence_reboot_timestamp, Instant.now()).getSeconds() < 8) { return; }
  val t = c3_temp_out.state
  if (t < 38) {
    if (room_heated60_flag) room_heated60_flag = false
    if (room_heated75_flag) room_heated75_flag = false
  }
  if (!room_heated60_flag && t >= 60) {
    sendTelegram("bot1", "В парной " + t + " градусов!")
    room_heated60_flag = true
  }
  if (!room_heated75_flag && t >= 75) {
    sendTelegram("bot1", "В парной " + t + " градусов!!!")
    room_heated75_flag = true
  }
end
